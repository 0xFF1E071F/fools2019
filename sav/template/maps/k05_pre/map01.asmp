; Map01

MAP_01_WIDTH equ 16
MAP_01_HEIGHT equ 16

Map01_DataStruct:
    dw Map01_Events
    dw JustPlainRet
    dw Map01_Blocks
    dw Map01_Scripts
    dw Map01_Encounters
    dw Map01_Trainers

Map01_Events:
    db 0, 0
    db 2 ; warps
    warp_event 25, 7, MAP_08, 2
    warp_event 5, 7, MAP_07, 1
    db 3 ; coord events
    coord_event 5, 17, $ff, MapCallScriptInSRAM
    coord_event 6, 17, $ff, MapCallScriptInSRAM
    coord_event 7, 17, $ff, MapCallScriptInSRAM
    db 0 ; bg events
    db 5 ; obj events
    object_event 13, 24, SPRITE_COOLTRAINER_F, SPRITEMOVEDATA_STANDING_DOWN, 0, 0, -1, -1, PAL_NPC_RED, OBJECTTYPE_TRAINER, 4, TrainerHeader_01, -1
    object_event 9, 11, SPRITE_SUPER_NERD, SPRITEMOVEDATA_SPINCLOCKWISE, 0, 0, -1, -1, PAL_NPC_RED, OBJECTTYPE_TRAINER, 4, TrainerHeader_02, -1
    object_event 17, 22, SPRITE_ROCKET, SPRITEMOVEDATA_SPINRANDOM_FAST, 0, 0, -1, -1, 0, OBJECTTYPE_SCRIPT, 0, MapCallScriptInSRAM, EVENT_K5_PRE_ROCKETS_DEFEATED
    object_event 18, 23, SPRITE_ROCKET, SPRITEMOVEDATA_SPINRANDOM_FAST, 0, 0, -1, -1, 0, OBJECTTYPE_SCRIPT, 0, MapCallScriptInSRAM, EVENT_K5_PRE_ROCKETS_DEFEATED
    object_event 19, 8, SPRITE_TEACHER, SPRITEMOVEDATA_WALK_LEFT_RIGHT, 2, 0, -1, -1, PAL_NPC_GREEN, OBJECTTYPE_SCRIPT, 0, MapCallScriptInSRAM, -1

Map01_Blocks:
    incbin "maps/map01.blk"

Map01_Scripts:
    rawtxt_ptr Map01_Trainer01_Encounter
    rawtxt_ptr Map01_Trainer01_Defeat
    hybrid_ptr Map01_Trainer01_Defeat
    rawtxt_ptr Map01_Trainer02_Encounter
    rawtxt_ptr Map01_Trainer02_Defeat
    hybrid_ptr Map01_Trainer02_Encounter
    hybrid_ptr Map01_RocketsText
    unused_ptr
    unused_ptr
    hybrid_ptr Map01_RocketsText
    unused_ptr
    unused_ptr
    hybrid_ptr Map01_NPC1
    unused_ptr
    unused_ptr
    script_ptr Map01_PlayerNameCall
    script_ptr Map01_PlayerNameCall
    script_ptr Map01_PlayerNameCall

Map01_NPC1:
    tx_far 1, _Map01_NPC1

Map01_RocketsText:
    text "Oh no!"
    next "Someone noticed us!"
    para "Quick! We'll just pretend"
    next "we're placeholder sprites!"
    para "Er-hem!"
    next "''Object event''!"
    done

Map01_Trainer02_Encounter:
    text "Shoutouts to Simpleflips!"
    done

Map01_Trainer02_Defeat:
    text "i got souped"
    done

Map01_Trainer01_Encounter:
    tx_far 1, _Map01_Trainer01_Encounter

Map01_Trainer01_Defeat:
    text "Why are you fighting me?"
    next "Go fight Team Rocket!"
    done

Map01_PlayerNameCall:
    rel_base Map01_PlayerNameCall
    checkevent EVENT_K5_PRE_NAME_REGISTERED
    iftrue_rel .end
    special UpdateSprites
    playmusic 0
    playsound SFX_CALL
    pause 30
    playsound SFX_CALL
    pause 30
    playsound SFX_TALLY
    opentext
    writetext_vwf Map01_PlayerNameCallText1
    pause 5
    playmusic MUSIC_MOBILE_ADAPTER_MENU
    writetext_vwf Map01_PlayerNameCallText2
.naming
    callasm SwitchToSRA3
    callasm Map01_PlayerNamingScreen_Trampoline
    callasm SwitchToSRA3
    callasm Map01_PlayerNaming_VerifyAndConvert
    ifequal_rel $50, .nameNotEntered
    ifequal_rel $51, .oneCharName
    ifequal_rel $52, .invalidChars
    writetext_vwf Map01_PlayerNameCall_IsThatOK
    callasm SwitchToSRA2
    callasm HandleRoofGraphics
    callasm GenericYesNoTextbox
    iffalse_rel .naming
    writetext_vwf Map01_PlayerNameCall_ThankYou
    playmusic 0
    writetext_vwf Map01_PlayerNameCall_Click
    playsound SFX_HANG_UP
    pause 75
    closetext
    loadvar wMapMusic, 0
    playmapmusic
    setevent EVENT_K5_PRE_NAME_REGISTERED
    setevent EVENT_K5_PRE_ROCKETS_DEFEATED_NEG
.end
    end
.nameNotEntered
    writetext_vwf Map01_PlayerNameCall_NoNameText
    jump_rel .naming
.oneCharName
    writetext_vwf Map01_PlayerNameCall_OneCharText
    jump_rel .naming
.invalidChars
    writetext_vwf Map01_PlayerNameCall_InvalidCharsText
    jump_rel .naming

Map01_PlayerNameCall_Click:
    text "Click!"
    done

Map01_PlayerNameCall_ThankYou:
    tx_far 1, _Map01_PlayerNameCall_ThankYou

Map01_PlayerNameCall_NoNameText:
    text "No... come on!"
    next "You must have a name!"
    para "Please, my research won't"
    next "ever proceed without it! "
    wait

Map01_PlayerNameCall_OneCharText:
    text "Seriously? One character?"
    next "This isn't a speedrun!"
    para "Please, my research won't"
    next "ever proceed without your"
    cont "name!"
    wait

Map01_PlayerNameCall_InvalidCharsText:
    tx_far 1, _Map01_PlayerNameCall_InvalidCharsText

Map01_PlayerNameCall_IsThatOK:
    text "OK, so let me verify."
    next "You're <B>"
    tx_buf wPlayerFullName
    text "</B>?"
    done

Map01_PlayerNaming_VerifyAndConvert:
    call .conv
    ld [wScriptVar], a
    ret
.conv
    ld hl, wPlayerFullName
    ld bc, 11
    xor a
    call ByteFill
    ld hl, wStringBuffer1
    ld a, [hli]
    cp $50
    ret z
    ld a, [hl]
    cp $50
    ld a, $51
    ret z
    dec hl
    ld de, wPlayerFullName
    ld c, 10
.loop
    ld a, [hli]
    cp $50
    jr z, .finished
    call Map01_PlayerNaming_MapCharacter
    cp $50
    jr z, .invalidChar
    ld [de], a
    inc de
    dec c
    jr nz, .loop
.finished
    xor a
    ret
.invalidChar
    ld a, $52
    ret

Map01_PlayerNaming_MapCharacter:
    push hl
    push de
    push bc
    ld b, a
    ld hl, .from
    ld de, .to
.loop
    ld a, [de]
    inc de
    ld c, a
    ld a, [hli]
    cp $50
    jr z, .out
    cp b
    jr nz, .loop
    ld a, c
.out
    pop bc
    pop de
    pop hl
    ret
.from
    db $80,$81,$82,$83,$84,$85,$86,$87,$88,$89,$8a,$8b,$8c,$8d,$8e,$8f,$90,$91
    db $92,$93,$94,$95,$96,$97,$98,$99,$a0,$a1,$a2,$a3,$a4,$a5,$a6,$a7,$a8,$a9
    db $aa,$ab,$ac,$ad,$ae,$af,$b0,$b1,$b2,$b3,$b4,$b5,$b6,$b7,$b8,$b9,$e7,$f4
    db $e6,$00,$e3,$e8,$9c,$9a,$9b,$7f,$01,$f0,$e0,$f3,$f6,$f7,$f8,$f9,$fa,$fb
    db $fc,$fd,$fe,$ff,$50
.to
    db $01,$02,$03,$04,$05,$06,$07,$08,$09,$0a,$0b,$0c,$0d,$0e,$0f,$10,$11,$12
    db $13,$14,$15,$16,$17,$18,$19,$1a,$1b,$1c,$1d,$1e,$1f,$20,$21,$22,$23,$24
    db $25,$26,$27,$28,$29,$2a,$2b,$2c,$2d,$2e,$2f,$30,$31,$32,$33,$34,$35,$36
    db $37,$38,$39,$3a,$3b,$3c,$3d,$3e,$3f,$40,$41,$42,$43,$44,$45,$46,$47,$48
    db $49,$4a,$4b,$4c

Map01_PlayerNameCallText1:
    tx_far 1, _Map01_PlayerNameCallText1

Map01_PlayerNameCallText2:
    tx_far 1, _Map01_PlayerNameCallText2

Map01_PlayerNamingScreen_Trampoline:
    prepare_sram_call 0, PlayerNamingScreen
    jp CallInSRAMBank

Map01_Trainers:
    dw Map01_Trainer01
    dw Map01_Trainer02

Map01_Trainer01:
    trainer_name ""
    trainer_roster TRAINERTYPE_NORMAL
    db {lv_rel -5}, {basic_mon}
    db {lv_rel -3}, {basic_mon}
    db {lv_rel -3}, {first_stage_mon}
    db -1

Map01_Trainer02:
    trainer_name ""
    trainer_roster TRAINERTYPE_NORMAL
    db {lv_rel -5}, {basic_mon}
    db {lv_rel -3}, {basic_mon}
    db -1

Map01_Encounters:
    db {rnd_basic_mon 40}, {lv_rel -5}
    db {rnd_basic_mon 40}, {lv_rel -4}
    db {rnd_basic_mon 41}, {lv_rel -4}
    db {rnd_basic_mon 41}, {lv_rel -3}
    db {rnd_basic_mon 42}, {lv_rel -6}
    db {rnd_basic_mon 42}, {lv_rel -5}
    db {rnd_basic_mon 43}, {lv_rel -5}
    db {rnd_basic_mon 43}, {lv_rel -4}
    db {rnd_basic_mon 44}, {lv_rel -6}
    db {rnd_basic_mon 44}, {lv_rel -6}
    db {rnd_basic_mon 45}, {lv_rel -5}
    db {rnd_basic_mon 45}, {lv_rel -5}
    db {rnd_basic_mon 46}, {lv_rel -3}
    db {rnd_basic_mon 46}, {lv_rel -6}
    db {rnd_basic_mon 47}, {lv_rel -5}
    db {rnd_basic_mon 47}, {lv_rel -4}
